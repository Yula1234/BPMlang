struct Generator {
	asm_file:FILE,
	scopes_vi:DynamicArray,
	scopes:DynamicArray,
	vars:DynamicArray,
	vars_index:int,
	arena:ArenaAllocator,
	label_count:int,
	procs:DynamicArray
}

struct Var(__global_obj_alloc) {
	name:ptr,
	stack_loc:int
}

struct Proc(__global_obj_alloc) {
	name:ptr,
	type:int
}

proc gen_var_lookup name:ptr gen:Generator -> int;

proc gen_proc_lookup name:ptr gen:Generator -> int {
	let procs = gen.procs;
	foreach_dynarray(procs, Proc, {
		if(streq(__value.name, name)) {
			return cast(int, __value);
		}
	})
	return 0;
}

proc gen_type_of_expr expr:NodeExpr gen:Generator -> int {
	if(expr.kind == NodeKindInt) {
		return DataTypeInt;
	}
	elif(expr.kind == NodeKindIdent) {
		let expr_ident = cast(NodeExprIdent, expr.data);
		let svar = gen_var_lookup(expr_ident.ident, gen);
		if(svar == 0) {
			let etok = expr_ident.def;
			format_print("%s:%d:%d ERROR: unkown name `%s`\n", etok.file_loc, etok.line, etok.col, expr_ident.ident);
			exit(1);
		}
		let var = cast(Var, svar);
		puts("get type of expr ident is not implemented yet\n");
		exit(1);
	}
	elif(expr.kind == NodeKindPlus) {
		let add_expr = cast(NodeExprAdd, expr.data);
		return gen_type_of_expr(cast(NodeExpr, add_expr.left), gen);
	}
	elif(expr.kind == NodeKindMinus) {
		let sub_expr = cast(NodeExprSub, expr.data);
		return gen_type_of_expr(cast(NodeExpr, sub_expr.left), gen);
	}
	elif(expr.kind == NodeKindMul) {
		let mul_expr = cast(NodeExprMul, expr.data);
		return gen_type_of_expr(cast(NodeExpr, mul_expr.left), gen);
	}
	elif(expr.kind == NodeKindDiv) {
		let div_expr = cast(NodeExprMul, expr.data);
		return gen_type_of_expr(cast(NodeExpr, div_expr.left), gen);
	}
	else {
		format_print("ERROR: unkown expr.kind at `gen_type_of_expr` - ");
		putu(expr.kind);
		exit(1);
	}
}

proc gen_var_lookup name:ptr gen:Generator -> int {
	let i = 0;
	let j = 0;
	let scopes_size = gen.vars.size;
	while(i < scopes_size) {
		let cur_scope = cast(DynamicArray, dynarray_get(gen.vars, i));
		while(j < cur_scope.size) {
			let var = cast(Var, dynarray_get(cur_scope, j));
			if(streq(var.name, name)) {
				return cast(int, var);
			}
			j += 1;
		}
		j = 0;
		i += 1;
	}
	return 0;
}

proc new_generator asm_file:FILE -> Generator {
	return Generator(
		asm_file,
		new_dynarray(12),
		new_dynarray(12),
		new_dynarray(12),
		0,
		new_arena(56 * 1024),
		0,
		new_dynarray(12)
	);
}

proc delete_generator gen:Generator -> void {
	closef(gen.asm_file);
	delete_dynarray(gen.vars);
	delete_dynarray(gen.scopes);
	delete_dynarray(gen.scopes_vi);
	delete_dynarray(gen.procs);
	arena_delete(gen.arena);
	delete gen;
}

proc gen_generate_expr expr:NodeExpr gen:Generator -> void {
	if(expr.kind == NodeKindInt) {
		format_fprint(gen.asm_file.desc, "    push dword %s\n", expr.data);
	}
	elif(expr.kind == NodeKindIdent) {
		let expr_ident = cast(NodeExprIdent, expr.data);
		let svar = gen_var_lookup(expr_ident.ident, gen);
		if(svar == 0) {
			let etok = expr_ident.def;
			format_print("%s:%d:%d ERROR: unkown name `%s`\n", etok.file_loc, etok.line, etok.col, expr_ident.ident);
			exit(1);
		}
		let var = cast(Var, svar);
		format_fprint(gen.asm_file.desc, "    push dword [ebp-%d]\n", var.stack_loc);
	}
	elif(expr.kind == NodeKindPlus) {
		let bin_add = cast(NodeExprAdd, expr.data);
		gen_generate_expr(cast(NodeExpr, bin_add.left), gen);
		gen_generate_expr(cast(NodeExpr, bin_add.right), gen);
		fputs(gen.asm_file.desc, "    pop ebx\n");
		fputs(gen.asm_file.desc, "    pop eax\n");
		fputs(gen.asm_file.desc, "    add eax, ebx\n");
		fputs(gen.asm_file.desc, "    push eax\n");
	}
	elif(expr.kind == NodeKindMinus) {
		let bin_sub = cast(NodeExprSub, expr.data);
		gen_generate_expr(cast(NodeExpr, bin_sub.left), gen);
		gen_generate_expr(cast(NodeExpr, bin_sub.right), gen);
		fputs(gen.asm_file.desc, "    pop ebx\n");
		fputs(gen.asm_file.desc, "    pop eax\n");
		fputs(gen.asm_file.desc, "    sub eax, ebx\n");
		fputs(gen.asm_file.desc, "    push eax\n");
	}
	elif(expr.kind == NodeKindMul) {
		let mul_expr = cast(NodeExprMul, expr.data);
		gen_generate_expr(cast(NodeExpr, mul_expr.left), gen);
		gen_generate_expr(cast(NodeExpr, mul_expr.right), gen);
		fputs(gen.asm_file.desc, "    pop ebx\n");
		fputs(gen.asm_file.desc, "    pop eax\n");
		fputs(gen.asm_file.desc, "    imul eax, ebx\n");
		fputs(gen.asm_file.desc, "    push eax\n");
	}
	elif(expr.kind == NodeKindDiv) {
		let div_expr = cast(NodeExprMul, expr.data);
		gen_generate_expr(cast(NodeExpr, div_expr.left), gen);
		gen_generate_expr(cast(NodeExpr, div_expr.right), gen);
		fputs(gen.asm_file.desc, "    pop ebx\n");
		fputs(gen.asm_file.desc, "    pop eax\n");
		fputs(gen.asm_file.desc, "    xor edx, edx\n");
		fputs(gen.asm_file.desc, "    div ebx\n");
		fputs(gen.asm_file.desc, "    push eax\n");
		fputs(gen.asm_file.desc, "    mov edx, ecx\n");
	}
	else {
		format_print("ERROR: unkown expr.kind at `gen_generate_expr` - ");
		putu(expr.kind);
		exit(1);
	}
}

/* variables tools */
proc gen_create_var gen:Generator name:ptr def:Token -> void {
	let var = Var(name, (gen.vars_index * 4) + 4);
	let i = gen.vars_index;
	gen.vars_index += 1;
	dynarray_push(cast(DynamicArray, dynarray_get(gen.vars, ((gen.vars).size) - 1)), var);
}

proc gen_generate_exit stmt:NodeStmtExit gen:Generator -> void {
	let expr = cast(NodeExpr, stmt.expr);
	gen_generate_expr(expr, gen);
	fputs(gen.asm_file.desc, "    call ExitProcess@4\n");
	fputs(gen.asm_file.desc, "    add esp, 4\n");
}

proc gen_generate_print stmt:NodeStmtPrint gen:Generator -> void {
	let expr = cast(NodeExpr, stmt.expr);
	gen_generate_expr(expr, gen);
	fputs(gen.asm_file.desc, "    push numfmt\n");
	fputs(gen.asm_file.desc, "    call printf\n");
	fputs(gen.asm_file.desc, "    add esp, 8\n");
}

proc gen_generate_let stmt:NodeStmtLet gen:Generator -> void {
	gen_create_var(gen, stmt.name, stmt.def);
	gen_generate_expr(cast(NodeExpr, stmt.expr), gen);
	fputs(gen.asm_file.desc, "    pop edx\n");
	format_fprint(gen.asm_file.desc, "    mov dword [ebp-%d], edx\n", gen.vars_index * 4);
}

proc gen_create_label gen:Generator -> ptr {
	let str = arena_take(gen.arena, 12);
	store8(str, 'L');
	utos(str + 1, gen.label_count);
	gen.label_count += 1;
	return str;
}

proc collect_allign block:DynamicArray -> int;

proc gen_stmt _stmt:ptr gen:Generator -> void;

proc gen_begin_scope fsz:int gen:Generator -> void {
	if(fsz != 0) {
		format_fprint(gen.asm_file.desc, "    sub esp, %d\n", fsz * 4);
	}
	dynarray_push(gen.scopes, fsz);
	dynarray_push(gen.scopes_vi, gen.vars_index);
	dynarray_push(gen.vars, new_dynarray(8));
}

proc gen_end_scope gen:Generator -> void {
	let scope_size = cast(int, dynarray_get(gen.scopes, (gen.scopes.size) - 1));
	if(scope_size != 0) {
		format_fprint(gen.asm_file.desc, "    add esp, %d\n", scope_size * 4);
	}
	dynarray_pop(gen.scopes);
	let old_vi = dynarray_pop(gen.scopes_vi);
	gen.vars_index = old_vi;
	dynarray_pop(gen.vars);
}

proc gen_block block:NodeStmtBlock gen:Generator -> void {
	let i = 0;
	gen_begin_scope(collect_allign(block.stmts), gen);
	while(i < block.stmts.size) {
		gen_stmt(cast(ptr, dynarray_get(block.stmts, i)), gen);
		i += 1;
	}
	gen_end_scope(gen);
}

proc gen_if_pred pred:NodeIfPred endlab:ptr gen:Generator -> void {
	if(pred.kind == PredKindElse) {
		let else_pred = cast(IfPredElse, pred.data);
		gen_block(else_pred.block, gen);
	}
	elif(pred.kind == PredKindElif) {
		let elif_pred = cast(IfPredElif, pred.data);
		gen_generate_expr(elif_pred.condition, gen);
		let elselab = gen_create_label(gen);
		fputs(gen.asm_file.desc, "    pop eax\n");
		fputs(gen.asm_file.desc, "    test eax, eax\n");
		format_fprint(gen.asm_file.desc, "    jz %s\n", elselab);
		gen_block(elif_pred.block, gen);
		format_fprint(gen.asm_file.desc, "    jmp %s\n", endlab);
		format_fprint(gen.asm_file.desc, "    %s:\n", elselab);
		if(cast(int, elif_pred.pred) != 0) {
			let pred = cast(NodeIfPred, elif_pred.pred);
			gen_if_pred(pred, endlab, gen);
		}
	}
	else {
		format_print("gen_if_pred unkown if pred kind %d\n", pred.kind);
	}
}

proc gen_generate_if stmt:NodeStmtIf gen:Generator -> void {
	gen_generate_expr(stmt.condition, gen);
	let endlab = gen_create_label(gen);
	let elselab = gen_create_label(gen);
	fputs(gen.asm_file.desc, "    pop eax\n");
	fputs(gen.asm_file.desc, "    test eax, eax\n");
	format_fprint(gen.asm_file.desc, "    jz %s\n", elselab);
	gen_block(stmt.block, gen);
	format_fprint(gen.asm_file.desc, "    jmp %s\n", endlab);
	format_fprint(gen.asm_file.desc, "    %s:\n", elselab);
	if(cast(int, stmt.pred) != 0) {
		let pred = cast(NodeIfPred, stmt.pred);
		gen_if_pred(pred, endlab, gen);
	}
	format_fprint(gen.asm_file.desc, "    %s:\n", endlab);
}

proc gen_define_proc name:ptr type:int gen:Generator -> void {
	dynarray_push(gen.procs, Proc(name, type));
}

proc gen_generate_proc stmt:NodeStmtProc gen:Generator -> void {
	gen_define_proc(stmt.name, stmt.type, gen);
	format_fprint(gen.asm_file.desc, "%s:\n", stmt.name);
	fputs(gen.asm_file.desc, "    push ebp\n");
	fputs(gen.asm_file.desc, "    mov ebp, esp\n");
	gen_block(stmt.block, gen);
	fputs(gen.asm_file.desc, "    pop ebp\n");
	fputs(gen.asm_file.desc, "    ret\n\n");
}

cextern "sprintf";

proc sprintf p:ptr fstr:ptr -> void [nosizedargs];

proc gen_generate_call stmt:NodeStmtCall gen:Generator -> void {
	let _procedure = gen_proc_lookup(stmt.name, gen);
	if(_procedure == 0) {
		let def = stmt.def;
		let errmes = malloc(120);
		sprintf(errmes, "unkown procedure `%s`", stmt.name);
		Error(errmes, def.file_loc, def.line, def.col);
	}
	format_fprint(gen.asm_file.desc, "    call %s\n", stmt.name);	
}

proc gen_stmt _stmt:ptr gen:Generator -> void {
	let stmt = cast(NodeStmt, _stmt);
	if(stmt.kind == StmtKindExit) {
		let stmt_exit = cast(NodeStmtExit, stmt.data);
		gen_generate_exit(stmt_exit, gen);
	}
	elif(stmt.kind == StmtKindPrint) {
		let stmt_print = cast(NodeStmtPrint, stmt.data);
		gen_generate_print(stmt_print, gen);
	}
	elif(stmt.kind == StmtKindLet) {
		let stmt_let = cast(NodeStmtLet, stmt.data);
		gen_generate_let(stmt_let, gen);
	}
	elif(stmt.kind == StmtKindIf) {
		let stmt_if = cast(NodeStmtIf, stmt.data);
		gen_generate_if(stmt_if, gen);
	}
	elif(stmt.kind == StmtKindProc) {
		let stmt_proc = cast(NodeStmtProc, stmt.data);
		gen_generate_proc(stmt_proc, gen);
	}
	elif(stmt.kind == StmtKindCall) {
		let stmt_call = cast(NodeStmtCall, stmt.data);
		gen_generate_call(stmt_call, gen);
	}
	else {
		puts("unkown stmt at gen_stmt\n");
		exit(1);
	}
}

proc collect_allign block:DynamicArray -> int {
	let i = 0;
	let allign = 0;
	while(i < block.size) {
		let stmt = cast(NodeStmt, dynarray_get(block, i));
		if(stmt.kind == StmtKindLet) {
			allign += 1;
		}
		i += 1;
	}
	return allign;
}

proc gen_generate_header ast:ptr gen:Generator -> void {
	fputs(gen.asm_file.desc, "section .text\n\n");
	fputs(gen.asm_file.desc, "global main\n\n");
	fputs(gen.asm_file.desc, "extern printf\n");
	fputs(gen.asm_file.desc, "extern ExitProcess@4\n\n");
}

proc gen_generate_footer gen:Generator -> void {
	fputs(gen.asm_file.desc, "\nsection .data\n");
	fputs(gen.asm_file.desc, "    numfmt: db \"%d\", 0xa, 0x0\n");
}

proc gen_generate ast:ptr gen:Generator -> void {
	let ast_level = 0;
	gen_generate_header(ast, gen);
	while(rd32(ast +  (ast_level * 4)) != 0) {
		gen_stmt(cast(ptr, rd32(ast +  (ast_level * 4))), gen);
		ast_level = ast_level + 1;
	}
	gen_generate_footer(gen);
}